/*
 * Instantiation of SHAKE256, a SHA-3 extendable-output function, as specified
 * in [FIPS-202].
 *
 * [FIPS-202]: National Institute of Standards and Technology. SHA-3 Standard:
 *     Permutation-Based Hash and Extendable-Output Functions. (Department of
 *     Commerce, Washington, D.C.), Federal Information Processing Standards
 *     Publication (FIPS) NIST FIPS 202. August 2015.
 *     @see https://dx.doi.org/10.6028/NIST.FIPS.202
 *
 * @copyright Galois, Inc.
 * @author Ajay Kumar Eeralla
 * @editor Marcella Hastings <marcella@galois.com>
 *
 */
module Primitive::Keyless::Hash::SHA3::Instantiations::SHAKE256 where
import Primitive::Keyless::Hash::SHA3::Specification where
    type b = 1600
    type nr = 24
    // The capacity is double the security level, so this provides a security
    // level of 256 (as the name suggests).
    type c = 512
import Primitive::Keyless::Hash::SHA3::KeccakBitOrdering as KBO

/**
 * SHAKE256 extendable-output function.
 * [FIPS-202] Section 6.2.
 *
 * This supports any output length `d`, including infinite length.
 *
 * Note that the specification of `c` is above, in the instantiation of the
 * `keccak` module.
 *
 * This expects input and produces output in the bit ordering used by the
 * `Keccak` spec, where bytes are in MSB order and the bits in each byte are
 * in LSB order. Use the `KeccakBitOrdering::toBytes` function to transform
 * the input and output to Cryptol-standard MSB order:
 * ```repl
 * join (KBO::toBytes (shake256 (join (KBO::toBytes 0x6ec9b3)))) == 0x0adc999bb92b9fdd0ce7664a06081ef8
 * ```
 */
shake256 : {d, m} (fin m) => [m] -> [d]
shake256 M = Keccak (M # 0b1111)

/**
 * SHAKE256 extendable-output function.
 * [FIPS-202] Section 6.2.
 *
 * This supports any output length `d`, including infinite length.
 *
 * This expects input and output in MSB order. It handles conversion to and
 * from the bit ordering expected by `Keccak`. If input is provided in the
 * format described in [FIPS-202] Appendix B (a hex string with an even
 * number of digits and an actual length `n`), use the `KOB::truncate` function
 * to get the correct input:
 * ```repl
 * xof 0x6ec9b3 == 0x0adc999bb92b9fdd0ce7664a06081ef8
 * xof (KBO::truncate`{23} 0x3a7339) == 0xfafa506e40f7a356507196677a7b4c82
 * ```
 */
xof : {d, m} (fin m) => [m] -> [d]
xof M = KBO::reverseBitOrdering (Keccak ((KBO::reverseBitOrdering M) # 0b1111))

/**
 * SHAKE256 extendable-output function over byte-delimited inputs.
 *
 * Note that `d` is specified in _bits_. The `dBytes` parameter allows us to
 * have infinite output. The alternative approach, requiring `d % 8 == 0`,
 * is only possible when `d` is finite due to a type constraint on
 * the mod operation.
 */
xofBytes : {d, m, dBytes} (fin m, dBytes * 8 == d) => [m][8] -> [dBytes][8]
xofBytes M = split (xof (join M))
